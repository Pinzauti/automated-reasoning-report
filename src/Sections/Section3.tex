\section{ASP model}
As explained in~\cite{ClingoGuide} an ASP model is divided in four part: generate, define, test and display.
As for the generate part our Python program handles both the generation of the random instances in random mode and the inclusion of the user defined input in manual mode.
\subsection{Generate}
The input is of the form
\begin{minted}[breaklines, linenos]{text}
    boardl(D).
    number(X_1, Y_1, T_1).
    .
    .
    .
    number(X_n, Y_n, T_n).
\end{minted}
with \mintinline{text}{n} the number of starting points, \mintinline{text}{D} the dimension of the board and \mintinline{text}{X_i, Y_i} the coordinates of the \mintinline{text}{n} starting points. \mintinline{text}{T} is the number of turns each starting point has to make to arrive to the goal.

\subsection{Define}
The define part is displayed below
\begin{minted}[breaklines, linenos]{text}
    grid(1..D, 1..D) :-boardl(D).
    1 {goal(X,Y): grid(X,Y)} 1.
    
    {link(start(X1, Y1), end(X2, Y2)) :  grid(X1,Y1), grid(X2,Y2), |X1 - X2| + |Y1 - Y2| = 1}. 
    
    connected(start(X1,Y1) ,end(X2,Y2)) :- link(start(X1,Y1), end(X2,Y2)).
    connected(start(X1,Y1), end(X2,Y2)) :- connected(start(X1,Y1), end(Z1,Z2)), link(start(Z1,Z2),end(X2,Y2)). 
\end{minted}

In line 1 we define the grid, a matrix of dimension $DxD$. The goal point, i.e. the intersection point of our links is defined in line 3. We also define, in line 4, what a link is and at the end we define the connection predicate, based recursively on the link predicate.
\subsection{Test}
\begin{minted}[breaklines, linenos]{text}
    :- #count{grid(X,Y): number(X,Y, _)} < 2. % There can't be only one starting point.

    :- goal(X, Y), number(X, Y, _). % The goal point can't be one of the start points.
    
    :- goal(X, Y), not connected(start(X0, Y0), end(X,Y)), number(X0,Y0, _). % The goal must be connected with all the starting points.
    
    :- link(start(X0, Y0), end(X1, Y1)), number(X1, Y1, _). % There can't be a link ending in a starting point.
    
    :- link(start(X1,Y1), _), not link(_, end(X1,Y1)), not number(X1,Y1, _).% A link can only start when another link end or at the starting point.
    
    :- link(_, end(X2,Y2)), not link(start(X2,Y2), _), not goal(X2, Y2). % A link can only end if there is another link starting or at the goal.
    
    :- link(start(X, Y),end(X1, Y1)), link(start(X, Y), end(X2, Y2)), X1 != X2. % No two links from one starting point.
    
    :- link(start(X, Y),end(X1, Y1)), link(start(X, Y), end(X2, Y2)), Y1 != Y2. % No two links from one starting point.
    
    :- link(start(X0, Y0),end(X, Y)), link(start(X1, Y1), end(X, Y)), X0 != X1, not goal(X,Y). % There can't be intersections.
    
    :- link(start(X0, Y0),end(X, Y)), link(start(X1, Y1), end(X, Y)), Y0 != Y1, not goal(X,Y). % There can't be intersections.
    
    :- link(start(X1, Y1), end(X2, Y2)), link(start(X2,Y2), end(X1, Y1)). % Avoid loops.
    
    :- link(_, end(X1,Y1)), not connected(start(X1,Y1), end(X, Y)), goal(X, Y), not goal(X1,Y1). % Links have to be connected to the goal.
    
    :- #count{grid(X2,Y2), grid(X3, Y3): link(start(X1, Y1), end(X2, Y2)), link(start(X2, Y2), end(X3, Y3)), |X1-X3| = 1, |Y1-Y3| = 1, connected(start(X0, Y0), end(X1, Y1));
    grid(X2,Y2), grid(X3, Y3): link(start(X0, Y0), end(X2, Y2)), link(start(X2, Y2), end(X3, Y3)), |X0-X3| = 1, |Y0-Y3| = 1} != Z, number(X0, Y0, Z). % Number of turns per each path should be as perscribed in each starting point.
    
\end{minted}

\subsection{Display}

Finally with the last two lines we display our solution, hiding everything except the goal and the link towards it.